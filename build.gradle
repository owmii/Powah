buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        maven { url = 'https://repo.spongepowered.org/maven' }
        jcenter()
        mavenCentral()
        maven { url = 'https://repo.spongepowered.org/repository/maven-public/' }
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
        classpath group: 'org.spongepowered', name: 'mixingradle', version: '0.7-SNAPSHOT'
    }
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'idea'
apply plugin: 'org.spongepowered.mixin'

version = "${System.env.VERSION}"
group = 'owmii.powah'
archivesBaseName = 'powahovercharged'

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'

minecraft {
    mappings channel: 'snapshot', version: '20201028-1.16.3'

    runs {
        client {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            arg "-mixin.config=powah.mixin.json"

            mods {
                powah {
                    source sourceSets.main
                }
            }
        }
        server {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            mods {
                powah {
                    source sourceSets.main
                }
            }
        }
        data {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            args '--mod', 'powah', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')

            mods {
                powah {
                    source sourceSets.main
                }
            }
        }
    }

    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')
}

sourceSets.main.resources { srcDirs = ["src/main/resources", "src/generated/resources"] }

repositories {
    maven { url = "https://maven.blamejared.com/" } //Jei
    maven { url = "https://maven.theillusivec4.top" } //Curios
    maven {
        name "cursemaven"
        url "https://cursemaven.com"
    }
    maven { url 'https://maven.blamejared.com' } //Patchouli is a mod by Vazkii
    maven { // Arch
        url = "https://maven.architectury.dev"
        content {
            includeGroup "me.shedaniel"
            includeGroup "me.shedaniel.cloth"
        }
    }
    maven { // KJS
        url = "https://maven.saps.dev/minecraft"
        content {
            includeGroup "dev.latvian.mods"
        }
    }
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.16.5-36.2.34'

    compileOnly fg.deobf("mezz.jei:jei-1.16.5:7.7.1.153:api")
    runtimeOnly fg.deobf("mezz.jei:jei-1.16.5:7.7.1.153")
    implementation fg.deobf("top.theillusivec4.curios:curios-forge:1.16.5-4.0.5.3")
    implementation fg.deobf("curse.maven:lollipop-347954:3232534")
    compileOnly fg.deobf("vazkii.patchouli:Patchouli:1.16.4-53.3:api")
    runtimeOnly fg.deobf("vazkii.patchouli:Patchouli:1.16.4-53.3")
    implementation fg.deobf("me.shedaniel:architectury-forge:1.22.32")
    implementation fg.deobf("dev.latvian.mods:kubejs-forge:1605.3.19-build.229")
    implementation fg.deobf("dev.latvian.mods:rhino-forge:1605.1.5-build.75")

    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'
}

tasks.withType(Copy).all { duplicatesStrategy 'exclude' }

mixin {
    add sourceSets.main, "powah.mixin-refmap.json"
}

jar {
    manifest {
        attributes([
                'FMLAT'                   : 'accesstransformer.cfg',
                "TweakClass"              : "org.spongepowered.asm.launch.MixinTweaker",
                "TweakOrder"              : 0,
                "MixinConfigs"            : "powah.mixin.json",
                "Specification-Title"     : mod_name,
                "Specification-Vendor"    : "owmii",
                "Specification-Version"   : mod_version,
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : "${version}",
                "Implementation-Vendor"   : "owmii",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ],)
    }

    finalizedBy 'reobfJar'
}
